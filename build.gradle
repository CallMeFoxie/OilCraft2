def VersionMajor = 0
def VersionMinor = 1
def VersionFix = 0

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

def VersionBuild = 0
if (System.getenv("BUILD_NUMBER") != null)
    VersionBuild = System.getenv("BUILD_NUMBER")

version = "1.7.10-${VersionMajor}.${VersionMinor}-${VersionBuild}"
if (VersionFix != 0) 
    version = "1.7.10-${VersionMajor}.${VersionMinor}.${VersionFix}-${VersionBuild}"
    
group = "cz.ondraster.oilcraft2"
archivesBaseName = "OilCraft2"

minecraft {
    version = "1.7.10-10.13.3.1373-1.7.10"
    runDir = "run"
    replace '@VERSION@', project.version
}

dependencies {
}

processResources
        {
            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft.version

            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'

                expand 'version': project.version, 'mcversion': project.minecraft.version
            }
            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
            }
        }


task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task apiJar(type: Jar) {
    from(sourceSets.main.java) {
        include "cz/ondraster/oilcraft2/api/"
    }
    classifier = 'api'
}

artifacts {
    archives deobfJar
    archives apiJar
}

build.dependsOn deobfJar, apiJar